# Maintainer: Enlightenment Developers <enlightenment-devel@enlightenment.org>
pkgname=('elementary' 'elementary_test' 'elementary_doc')
[[ -d ../.git ]] && pkgname=('elementary-git' 'elementary_test-git' 'elementary_doc-git')
pkgver=1.11.99.10566.b520960
pkgrel=1
pkgdesc="Enlightenment GUI toolkit - GIT development snapshot"
arch=('i686' 'x86_64' 'arm')
url="http://www.enlightenment.org"
license=('LGPL2.1' 'CCPL:cc-by-sa')
makedepends=('doxygen' 'imagemagick')
[[ -d ../.git ]] && makedepends+=('git')
depends=('efl')
  [[ ! $(pacman -T ewebkit) ]] && depends+=('ewebkit')                 # webkit-efl support is detected at build time
  [[ ! $(pacman -T elocation-git) ]] && depends+=('elocation-git')     # elocation support is detected at build time
  [[ ! $(pacman -T libeweather-git) ]] && depends+=('libeweather-git') # eweather support is detected at build time
optdepends=('emotion_generic_players' 'evas_generic_loaders')
options=('debug')

pkgver() {
  cd "../.."

  if [[ -d .git ]]; then
    local efl_version=$(grep -m 1 EFL_VERSION configure.ac | grep -o "[[:digit:]]*" | tr '\n' '.')
    efl_version=$(awk -F , -v efl_version=${efl_version%.} '/^AC_INIT/ {gsub(/efl_version/, efl_version); gsub(/[\[\] -]/, ""); print $2}' configure.ac)
    printf "$efl_version.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
  else
    ./configure --version | awk '/^elementary/ {gsub("-", ""); print $NF}'
  fi
}

build() {
  cd "../.."
  export CFLAGS="$CFLAGS -fvisibility=hidden -fomit-frame-pointer"
  [[ -e Makefile ]] && make clean distclean
  [[ -e autogen.sh ]] && _configure=autogen.sh || _configure=configure
  ./$_configure --prefix=/usr --disable-static --with-elementary-web-backend=none
  make
  make doc
}

#check() {
#  cd "../.."
#  make -k check
#}

package_elementary() {
  cd "../.."
  make -j1 DESTDIR="$pkgdir/" install
  install -Dm644 README "$pkgdir/usr/share/doc/${pkgname%-git}/README"
  install -Dm644 NEWS "$pkgdir/usr/share/doc/${pkgname%-git}/NEWS"
  install -Dm644 ChangeLog "$pkgdir/usr/share/doc/${pkgname%-git}/ChangeLog"
  install -Dm644 AUTHORS "$pkgdir/usr/share/doc/${pkgname%-git}/AUTHORS"
  install -Dm644 COMPLIANCE "$pkgdir/usr/share/doc/${pkgname%-git}/COMPLIANCE"
  install -Dm644 COPYING.images "$pkgdir/usr/share/licenses/$pkgname/COPYING.images"
  sed -n '1,/details./p' COPYING > "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  ln -s "../licenses/$pkgname/COPYING" "$pkgdir/usr/share/doc/${pkgname%-git}/COPYING"
  ln -s "../licenses/$pkgname/COPYING.images" "$pkgdir/usr/share/doc/${pkgname%-git}/COPYING.images"
  rm -rf "$pkgdir/usr/bin/"elementary_test
  rm -rf "$pkgdir/usr/lib/elementary/modules/"{test_entry,test_map}
  rm -rf "$pkgdir/usr/share/applications/elementary_test.desktop"
  rm -rf "$pkgdir/usr/share/elementary/"{examples,images,objects}
}

package_elementary-git() {
  provides=("elementary=$pkgver")
  conflicts=('elementary')
  package_elementary
}

package_elementary_test() {
  pkgdesc="Test application for Elementary"
  depends=(${pkgname/_test/})
  unset optdepends

  cd "../.."
  make -j1 DESTDIR="$pkgdir/" install
  rm -rf "$pkgdir/usr/include"
  rm -rf "$pkgdir/usr/share/eolian/include/elementary-1/"
  rm -rf "$pkgdir/usr/bin/"{elementary_codegen,elementary_config,elementary_quicklaunch,elementary_run,elm_prefs_cc}
  rm -rf "$pkgdir/usr/lib/"{cmake,edje,libelementary.so*,pkgconfig}
  rm -rf "$pkgdir/usr/lib/elementary/modules/"{access_output,datetime_input_ctxpopup,prefs}
  rm -rf "$pkgdir/usr/share/applications/elementary_config.desktop"
  rm -rf "$pkgdir/usr/share/elementary/"{config,examples,themes,edje_externals}
  rm -rf "$pkgdir/usr/share/"{icons,locale}
}

package_elementary_test-git() {
  provides=("elementary_test=$pkgver")
  conflicts=('elementary_test')
  package_elementary_test
}

package_elementary_doc() {
  pkgdesc="Documentation for Elementary"
  arch=('any')
  unset depends optdepends

  cd "../.."
  mkdir -p "$pkgdir/usr/share/doc/${pkgname%-git}"
  cp -a doc/html "$pkgdir/usr/share/doc/${pkgname%-git}/html"
  cp -a doc/latex "$pkgdir/usr/share/doc/${pkgname%-git}/latex"
}

package_elementary_doc-git() {
  package_elementary_doc
  provides=("elementary_doc=$pkgver")
  conflicts=('elementary_doc')
}
